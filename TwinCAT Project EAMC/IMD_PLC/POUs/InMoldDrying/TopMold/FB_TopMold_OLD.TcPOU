<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.12">
  <POU Name="FB_TopMold_OLD" Id="{12607023-8663-43a6-a062-ab85428ca54a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_TopMold_OLD
VAR CONSTANT
	sCycleStateTextDef: ARRAY[0..25]  OF STRING :=	
		['State 0: Wait for Start',
		'State 1: Wait for signal to start move down',
		'State 2: Move down',
		'State 3: Drying lock time, Building pressure',
		'State 4: Start drying time',
		'State 5: Open pressure relief valve',
		'State 6: Vacuum time closed',
		'State 7: Move up',
		'State 8: Wait until take out unit is moving up and start blowoff',
		'State 9: Await blowoff time and stop blowoff',
		'State 10:',
		'State 11:',
		'State 12:',
		'State 13:',
		'State 14:',
		'State 15:',
		'State 16:',
		'State 17:',
		'State 18:',
		'State 19:',
		'State 20: Stop cycle state',																
		'State 21: Stop cycle state',
		'State 22:',
		'State 23:',
		'State 24:',
		'State 25:Stop direct state'];
	
	tCycleStateTimeouts: ARRAY[0..10] OF TIME := 
		[T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,
		T#10S];

	sInitStateTextDef: ARRAY[0..10] OF STRING :=
		['State 0: Wait for Init Start',
		'State 1: Await pressure relief',
		'State 2: Move up',
		'State 3: Description Init State 3',
		'State 4: Description Init State 4',
		'State 5: Description Init State 5',
		'State 6: Description Init State 6',
		'State 7: Description Init State 7',
		'State 8: Description Init State 8',
		'State 9: Description Init State 9',
		'State 10: Stop init state'];

	tInitStateTimeouts: ARRAY[0..10] OF TIME := 
		[T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S
		,T#10S];
END_VAR

VAR_INPUT
	mxReset							: BOOL;	(*Reset Error*)
	mxEmergency						: BOOL;
	mxAutoMode						: BOOL; (* TRUE = Automode *)
	mxEmptyMachine					: BOOL;
	mxEnableHeating					: BOOL;

	mxResetCycleState				: BOOL;
	mxResetInitState				: BOOL;
	mxForceInit						: BOOL; (*Reset Calibration Flag*)

	mxMoveDown						: BOOL; (* Signal from bottommold *)
	mxMoveUp						: BOOL; (* Drying finished or transfer ready with new product *)
	mxTakeOutIsMovingUp: BOOL;
	RHSR_Blocked 					: BOOL;
END_VAR
VAR_IN_OUT
	MachParameters					: ST_TopMoldMachPar;
	ProdParameters					: ST_TopMoldProdPar;
END_VAR
VAR_OUTPUT
(*Errors*)
	Errors							: ST_TopMoldErrors;
	mxErrorInit						: BOOL;		(* Error, stop immediately. Initialisation neccesary *)
	mxErrorStopinPos				: BOOL;		(* Error, stop in position. Only reset neccesary *)

(* State vars *)
	mxCycleBusy						: BOOL;
	mxInitBusy						: BOOL;
	
(* Vars *)
	mxInitialized					: BOOL;
	
	mxIsUp							: BOOL;
	mxIsDown						: BOOL;
	mxCilIsMovingIn					: BOOL;
	mxCilIsMovingOut				: BOOL;
	mxCilIsIn						: BOOL;
	mxCilIsOut						: BOOL;	
END_VAR
VAR
(* State vars *)
	miCycleState					: INT :=0;
	miCycleOldstate					: INT :=0;
	mxCycleTransitionState			: BOOL := FALSE;
	sCycleStateText					: STRING;
	exCycleTimeOut					: BOOL;		(* Timeout for the whole programcycle *)
	CycleTimeout					: TON;
	CycleTimeOutUpFlank				: R_TRIG;
	mxCycleTimeoutEnable			: BOOL;
	CycleTime						: REAL;
	mxCycleHasError					: BOOL;
	miStopState						: INT;
	mxStartCycle					: BOOL;
	mxStopCycle						: BOOL;
	mxStopDirect					: BOOL;
	
	miInitState						: INT	:=0;
	miInitOldstate					: INT	:=0;
	mxInitTransitionState			: BOOL	:= FALSE;
	sInitStateText					: STRING;
	exInitTimeOut					: BOOL;		(* Timeout for the whole programcycle *)
	InitTimeout						: TON;
	InitTimeOutUpFlank				: R_TRIG;
	mxInitTimeoutEnable				: BOOL;
	InitTime						: REAL;
	mxInitHasError					: BOOL;
	mxStartInit						: BOOL;
	mxStopInit						: BOOL;
(* Objects *)
	HydraulicCilinder				: FB_HydraulicCilinderAnalog;
	Vacuum							: FB_MonoValveNoFdbck;
	BlowOff							: FB_MonoValveNoFdbck;
	HeatingZone						: ARRAY [1..6] OF FB_Heatingzone;
(* Vars *)
	T_DryingTime					: TON;
	T_BlowOffClosed					: TON;
	RT_TakeOutIsMovingUp			: R_TRIG;
	T_DryingMoldLock				: TON;
	T_VacuumTimeClosed				: TON;
	T_BlowOff						: TON;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Error();
Init();
Cycle();
Objects();
]]></ST>
    </Implementation>
    <Action Name="Cycle" Id="{efc7c7f2-e575-432e-8f92-c0d983c8a7bd}">
      <Implementation>
        <ST><![CDATA[(*StateChange (Transition)  Signalgeneration*)
mxCycleTransitionState:=(miCycleState<>miCycleOldstate);
(*State text*)
sCycleStateText:=sCycleStateTextDef[miCycleState];
(*Timeout Watchdog for Statemachine*)
mxCycleTimeoutEnable:=NOT (miCycleState=0) AND NOT mxReset;

CycleTimeout(IN:=mxCycleTimeoutEnable  , PT:=tCycleStateTimeouts[miCycleState] );
CycleTimeOutUpFlank(CLK:=CycleTimeout.Q );
miCycleOldstate:=miCycleState;

(*Set Errorbit when Raising edge of Timeout has been detected*)
IF CycleTimeOutUpFlank.Q THEN
	exCycleTimeOut:=TRUE; 			(* 1 Timeout for the whole programcycle *)
END_IF

(*Function to reset the Statemachine*)
IF mxResetCycleState THEN
	mxResetCycleState:= FALSE;
	mxInitialized := FALSE;
		
	miCycleState:=0;
	miCycleOldstate:=miCycleState;
	mxCycleTransitionState:=FALSE;

	mxStartCycle:= FALSE;
	mxStopCycle:= FALSE;
	mxStopDirect:=FALSE;
	mxIsDown := FALSE;
	mxIsUp := FALSE;
	mxMoveUp := FALSE;
	mxMoveDown := FALSE;

	T_DryingTime.IN:= FALSE;
	T_DryingMoldLock.IN:= FALSE;
	T_VacuumTimeClosed.IN:= FALSE;
	T_BlowOffClosed.IN:= FALSE;
	T_BlowOff.IN:=FALSE;	
	
	HydraulicCilinder.M_Stop();
END_IF

(*Definition State has Error*)
mxCycleHasError:=FALSE;

T_DryingTime(IN:= , PT:= MachParameters.DryingTime);
T_DryingMoldLock(IN:= , PT:= MachParameters.DryingPressureLockTime);
T_VacuumTimeClosed(IN:= , PT:= MachParameters.VacuumTimeClosed);
T_BlowOffClosed(IN:= (BlowOff.mxIsOn AND (miCycleState=3 OR miCycleState=4)) AND mxAutoMode, PT:= MachParameters.BlowOffTimeClosed);
T_BlowOff(IN:=, PT:=MachParameters.BlowOffToTakeOut);

IF T_BlowOffClosed.Q THEN BlowOff.M_Off(); END_IF


(*Cycle State's*)
CASE miCycleState OF

	(* Cycle:
	LEEG beginnen
		-wacht op signaal vanuit bottommold, naar beneden
		-Naar beneden om product te drogen
		-als bijna dicht dan extra druk opbouwen
		-Na droogtijd/transfer up wait time, relief presure
		-vacuum aan, omhoog, signal to bottom mold -> vac of blow on
		-signaal Mold is up -> bottom mold
		-Wacht op bottom mold is in
		-Wacht op takeout unit is omhoog (als enabled)
		-blow off aan

	*)
	0:
		IF mxStartCycle THEN
			mxStartCycle:= FALSE;

			miCycleState:= 1;
		END_IF

	(* wait for move down *)	
	1:
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			
			miCycleState:= 25;
		ELSIF mxStopCycle AND mxEmptyMachine THEN
			mxEmptyMachine:= FALSE;
			
			miCycleState:= 0;
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			
			miStopState:= miCycleState;
			miCycleState:= 0;
		ELSIF mxMoveDown THEN			
			mxMoveDown:= FALSE;
			mxMoveUp:= FALSE;
			
			mxIsUp:= FALSE;
			
			miCycleState:= 2;
		END_IF
		
	(* Move down *)
	2:
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			
			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF HydraulicCilinder.M_Move(EndPosition:=ProdParameters.PositionDownStop, NormalSpeed:=MachParameters.SpeedDownNormal, 
										SlowPosition:=ProdParameters.PositionDownStop + ProdParameters.PositionDownSlow, SlowSpeed:=MachParameters.SpeedDownSlow, 
										CreepPosition:=ProdParameters.PositionDownStop + ProdParameters.PositionDownCreep, CreepSpeed:=MachParameters.SpeedDownCreep) THEN
			BlowOff.M_On(); 			// Blowoff stops after delay ^ at top of cylce
			miCycleState:= 3;
		END_IF

	(* Drying Lock time, build up pressure *)
	3:
		T_DryingMoldLock.IN:= TRUE;
		
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			
			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF HydraulicCilinder.M_Move(EndPosition:=ProdParameters.PositionDownStop -50.0, NormalSpeed:=MachParameters.SpeedDownNormal,
										SlowPosition:= ProdParameters.PositionDownSlow, SlowSpeed:=MachParameters.SpeedDownSlow, 
										CreepPosition:=ProdParameters.PositionDownCreep, CreepSpeed:=MachParameters.SpeedDownCreep) OR T_DryingMoldLock.Q THEN
			HydraulicCilinder.M_Stop();
			
			T_DryingMoldLock.IN:= FALSE;
			
			mxIsDown:= TRUE;
			mxIsUp:=FALSE;
			
			miCycleState:= 4;
		END_IF
	
	(* start drying *)
	4:
		T_DryingTime(IN:= TRUE);

		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;

			miStopState:=miCycleState;
			miCycleState:= 20;

		ELSIF (mxMoveUp AND T_DryingTime.Q) THEN	// changed by bateu 20180601
			mxMoveUp:= FALSE;
			T_DryingTime(IN:= FALSE);
			
			// Switch vacuum on
			Vacuum.M_On();
			
			miCycleState:= 5;
		END_IF
		
	(* Vacuum time closed *)
	5:
		T_VacuumTimeClosed.IN:= TRUE;	
		
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			
			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF T_VacuumTimeClosed.Q THEN
			T_VacuumTimeClosed.IN:= FALSE;
			
			miCycleState:= 6;
		END_IF
		
	(* Move up *)
	6:
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;

			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF HydraulicCilinder.M_Move(EndPosition:= MachParameters.PositionUpStop,  NormalSpeed:= MachParameters.SpeedUpNormal, 
										SlowPosition:= MachParameters.PositionUpStop - MachParameters.PositionUpSlow, SlowSpeed:= MachParameters.SpeedUpSlow, 
										CreepPosition:=MachParameters.PositionUpStop - MachParameters.PositionUpCreep, CreepSpeed:=MachParameters.SpeedUpCreep) THEN
			mxIsUp:= TRUE;
			mxIsDown:= FALSE;
			mxMoveUp:= FALSE;
			
			miCycleState:= 7;
		END_IF
		
	(* Wait until takeout is moving up *)	
	7:
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			HydraulicCilinder.M_Stop();

			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF mxTakeOutIsMovingUp THEN
			Vacuum.M_Off();
			BlowOff.M_On();
			
			miCycleState:= 8;
		END_IF
		
	(* Await Blowoff*)	
	8:
		T_BlowOff.IN := TRUE;
	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;(*Stop direct*)
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			HydraulicCilinder.M_Stop();

			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF T_BlowOff.Q THEN
			BlowOff.M_Off();
			
			T_BlowOff.IN := FALSE;
			miCycleState:= 1;
		END_IF

	(* Stop Cycle, wait for pressure slow setpoint reached by rebotech*)
	20:
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			
			miCycleState:= 25;
		ELSIF TRUE THEN
 			
			miCycleState:= 21;
		END_IF

	(* Stop cycle, wait for start *)
	21:	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;

			miCycleState:= 25;
		ELSIF mxStartCycle THEN

			miCycleState:= miStopState;
		END_IF
	(* Stop Direct *)	
	25:
		mxStartCycle:= FALSE;
		mxStopCycle:= FALSE;
		mxInitialized:= FALSE;

		T_DryingTime.IN:= FALSE;
		
		HydraulicCilinder.M_StopDirect();
		Vacuum.M_Off(); (* When product do not switch off *)
		BlowOff.M_Off();
	
		miCycleState:= 0;

END_CASE

mxCycleBusy:= miCycleState<>0 AND NOT (miCycleState=21);]]></ST>
      </Implementation>
    </Action>
    <Action Name="Error" Id="{9fed989a-c5f4-4e97-8346-93c202e80970}">
      <Implementation>
        <ST><![CDATA[

(* ****************************************************************************** *)
mxErrorStopInPos:= HydraulicCilinder.mxErrorStopInPos;
mxErrorInit:= HydraulicCilinder.mxErrorInit
			OR HeatingZone[1].mxErrorInit
			OR HeatingZone[2].mxErrorInit
			OR HeatingZone[3].mxErrorInit
			OR HeatingZone[4].mxErrorInit
			OR HeatingZone[5].mxErrorInit
			OR HEatingZone[6].mxErrorInit;
]]></ST>
      </Implementation>
    </Action>
    <Action Name="Init" Id="{00dcca80-6985-4b0f-9286-e5ff5cf89d17}">
      <Implementation>
        <ST><![CDATA[(*StateChange (Transition)  Signalgeneration*)
mxInitTransitionState:=(miInitState<>miInitOldstate);
(*State text*)
sInitStateText:=sInitStateTextDef[miInitState];
(*Timeout Watchdog for Statemachine*)
mxInitTimeoutEnable:=NOT (miInitState=0)  AND NOT mxReset;

InitTimeout(IN:=mxInitTimeoutEnable  , PT:=tInitStateTimeouts[miInitState] );
InitTimeOutUpFlank(CLK:=InitTimeout.Q );
miInitOldstate:=miInitState;

(*Set Errorbit when Raising edge of Timeout has been detected*)
IF InitTimeOutUpFlank.Q THEN
	exInitTimeOut:=TRUE; 			(* 1 Timeout for the whole programcycle *)
END_IF

(*Function to reset the Statemachine*)
IF mxResetInitState THEN
	mxResetInitState:=FALSE;
	miInitState:=0;
	miInitOldstate:=miInitState;
	mxInitTransitionState:=FALSE;
	
	mxStartInit:= FALSE;
	mxStopInit:= FALSE;		
END_IF

(*Definition State has Error*)
mxInitHasError:=FALSE;

(*Init State's*)
CASE miInitState OF
	0:
		IF mxStartInit THEN
			mxStartInit:= FALSE;
			mxInitialized:=FALSE;
			
			Vacuum.M_Off();
			BlowOff.M_Off();

			miInitState:= 1;
		END_IF
		
	(* Move Up *)	
	1:
		IF mxStopInit THEN
			mxStopInit:= FALSE;

			miInitState:= 10;	
		END_IF
		
		IF HydraulicCilinder.ActPosition > MachParameters.PositionUpStop THEN // Move down
			IF HydraulicCilinder.M_Move(EndPosition:=MachParameters.PositionUpStop, NormalSpeed:= MachParameters.SpeedUpNormal, 
											SlowPosition:=MachParameters.PositionUpStop - MachParameters.PositionUpSlow, SlowSpeed:=MachParameters.SpeedUpSlow, 
											CreepPosition:=MachParameters.PositionUpStop - MachParameters.PositionUpCreep,	CreepSpeed:=MachParameters.SpeedUpCreep ) THEN
			mxInitialized:= TRUE; (* Return val *)
			miInitState:= 0;	
			END_IF
		ELSE
			IF HydraulicCilinder.M_Move(EndPosition:=MachParameters.PositionUpStop, NormalSpeed:= MachParameters.SpeedUpNormal, 
											SlowPosition:=MachParameters.PositionUpStop + MachParameters.PositionUpSlow, SlowSpeed:=MachParameters.SpeedUpSlow, 
											CreepPosition:=MachParameters.PositionUpStop + MachParameters.PositionUpCreep,	CreepSpeed:=MachParameters.SpeedUpCreep ) THEN
			mxInitialized:= TRUE; (* Return val *)
			miInitState:= 0;	
			END_IF
		END_IF

	(* Stop Init *)
	10:
		HydraulicCilinder.M_Stop();

		mxStartInit:= FALSE;
		mxStopInit:= FALSE;
		mxInitialized:=FALSE;					

		miInitState:= 0;
END_CASE

mxInitBusy:= miInitState<>0;]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_EmptyMachine" Id="{27910021-41c1-4a63-8ba3-ec2f3208a183}">
      <Declaration><![CDATA[METHOD M_EmptyMachine
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxCycleBusy THEN
	mxEmptyMachine:= TRUE;	
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartCycle" Id="{b74b68d9-0a5f-44e0-b923-7dde95c117fe}">
      <Declaration><![CDATA[METHOD M_StartCycle
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT mxCycleBusy THEN
	mxStartCycle:= TRUE;	
	mxStopCycle := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartInit" Id="{6e24aad9-3dfe-424b-9b9b-15513f156b44}">
      <Declaration><![CDATA[METHOD M_StartInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT mxInitBusy THEN 
	mxStartInit:= TRUE; 
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopCycle" Id="{bd8eaf1e-a3dc-4bac-8bf2-bc889cce4463}">
      <Declaration><![CDATA[METHOD M_StopCycle
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxCycleBusy THEN 
	mxStopCycle:= TRUE; 
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopDirect" Id="{83dfc892-7419-4c1e-a6f1-1efe211cc35f}">
      <Declaration><![CDATA[METHOD M_StopDirect
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxCycleBusy THEN
	mxStopDirect:= TRUE;	
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopInit" Id="{a96f3438-9240-4c23-bedd-9a4ca9fa6371}">
      <Declaration><![CDATA[METHOD M_StopInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxInitBusy THEN 
	mxStopInit:= TRUE; 
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Action Name="Objects" Id="{564e1988-4c27-4b84-b369-8a44ad57ddd9}">
      <Implementation>
        <ST><![CDATA[(* Object Calls *)
HydraulicCilinder(
	mxReset:= mxReset, 
	mxEmergency:= mxEmergency, 
	mxAutoMode:= mxAutoMode, 
	EndPositionIn:= MachParameters.PositionUpStop,
	EndPositionOut:= ProdParameters.PositionDownStop,
	OffsetPosition:= MachParameters.PositionOffsetZero,
	OffsetAlarmPosition:= MachParameters.PositionOffsetAlarm,
	PositionPhysicalMeasured:=MachParameters.PositionPhyscialMeasured,
	PositionPhysicalMeasuredOld:=MachParameters.PositionPhysicalMeasuredOld,
	PhysicalOffsetPos:=MachParameters.PhysicalOffsetPos,
	RHSR_Blocked:=RHSR_Blocked,
	mxIsMovingIn=> mxCilIsMovingIn, 
	mxIsMovingOut=> mxCilIsMovingOut, 
	ManSpeedCreepIn:=MachParameters.SpeedUpCreep,
	ManSpeedCreepOut:=MachParameters.SpeedDownCreep,
	ManSpeedSlowIn:=MachParameters.SpeedUpSlow,
	ManSpeedSlowOut:=MachParameters.SpeedDownSlow,
	ManSpeedIn:=MachParameters.SpeedUpNormal,
	ManSpeedOut:=MachParameters.SpeedDownNormal,
	mxIsIn=> mxCilIsIn, 
	mxIsOut=> mxCilIsOut,
	Errors=> Errors.Hydraulic);
	
Vacuum(
	mxReset:= mxReset, 
	mxEmergency:= mxEmergency, 
	mxAutoMode:= mxAutoMode);
	
BlowOff(
	mxReset:= mxReset, 
	mxEmergency:= mxEmergency, 
	mxAutoMode:= mxAutoMode);


HeatingZone[1](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[1],
	Errors=> Errors.HeatingZone[1]);
	
HeatingZone[2](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[2],
	Errors=> Errors.HeatingZone[2]);

HeatingZone[3](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[3],
	Errors=> Errors.HeatingZone[3]);

HeatingZone[4](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[4],
	Errors=> Errors.HeatingZone[4]);

HeatingZone[5](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[5],
	Errors=> Errors.HeatingZone[5]);

HeatingZone[6](
	mxReset:= mxReset,
	mxEmergency:=mxEmergency,
	mxEnableHeating:=mxEnableHeating,
	MachParameters:=MachParameters.HeatingZone[6],
	Errors=> Errors.HeatingZone[6]);]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_TopMold_OLD">
      <LineId Id="330" Count="3" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.Cycle">
      <LineId Id="763" Count="17" />
      <LineId Id="1097" Count="0" />
      <LineId Id="1099" Count="0" />
      <LineId Id="781" Count="3" />
      <LineId Id="1101" Count="2" />
      <LineId Id="1108" Count="0" />
      <LineId Id="1113" Count="0" />
      <LineId Id="1112" Count="0" />
      <LineId Id="1116" Count="0" />
      <LineId Id="1107" Count="0" />
      <LineId Id="1105" Count="0" />
      <LineId Id="1203" Count="3" />
      <LineId Id="1174" Count="1" />
      <LineId Id="788" Count="4" />
      <LineId Id="794" Count="3" />
      <LineId Id="1176" Count="0" />
      <LineId Id="1178" Count="0" />
      <LineId Id="798" Count="1" />
      <LineId Id="806" Count="58" />
      <LineId Id="1211" Count="0" />
      <LineId Id="1214" Count="0" />
      <LineId Id="1216" Count="0" />
      <LineId Id="1225" Count="0" />
      <LineId Id="867" Count="15" />
      <LineId Id="1218" Count="0" />
      <LineId Id="1221" Count="0" />
      <LineId Id="1223" Count="0" />
      <LineId Id="1173" Count="0" />
      <LineId Id="1170" Count="0" />
      <LineId Id="885" Count="2" />
      <LineId Id="1152" Count="0" />
      <LineId Id="888" Count="18" />
      <LineId Id="910" Count="1" />
      <LineId Id="915" Count="0" />
      <LineId Id="1210" Count="0" />
      <LineId Id="1208" Count="1" />
      <LineId Id="916" Count="1" />
      <LineId Id="937" Count="23" />
      <LineId Id="1201" Count="0" />
      <LineId Id="962" Count="5" />
      <LineId Id="1228" Count="0" />
      <LineId Id="1231" Count="0" />
      <LineId Id="1233" Count="0" />
      <LineId Id="968" Count="0" />
      <LineId Id="1110" Count="0" />
      <LineId Id="970" Count="22" />
      <LineId Id="1180" Count="2" />
      <LineId Id="1199" Count="1" />
      <LineId Id="1183" Count="10" />
      <LineId Id="1195" Count="0" />
      <LineId Id="1202" Count="0" />
      <LineId Id="1196" Count="2" />
      <LineId Id="993" Count="7" />
      <LineId Id="1002" Count="20" />
      <LineId Id="1207" Count="0" />
      <LineId Id="1025" Count="8" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.Error">
      <LineId Id="10" Count="1" />
      <LineId Id="2" Count="1" />
      <LineId Id="12" Count="5" />
      <LineId Id="4" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.Init">
      <LineId Id="60" Count="0" />
      <LineId Id="33" Count="19" />
      <LineId Id="204" Count="0" />
      <LineId Id="148" Count="0" />
      <LineId Id="197" Count="0" />
      <LineId Id="53" Count="4" />
      <LineId Id="84" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="30" Count="0" />
      <LineId Id="144" Count="0" />
      <LineId Id="161" Count="0" />
      <LineId Id="163" Count="0" />
      <LineId Id="196" Count="0" />
      <LineId Id="164" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="195" Count="0" />
      <LineId Id="165" Count="1" />
      <LineId Id="103" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="92" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="170" Count="0" />
      <LineId Id="159" Count="0" />
      <LineId Id="221" Count="1" />
      <LineId Id="150" Count="0" />
      <LineId Id="211" Count="0" />
      <LineId Id="208" Count="0" />
      <LineId Id="210" Count="0" />
      <LineId Id="156" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="223" Count="0" />
      <LineId Id="212" Count="0" />
      <LineId Id="214" Count="0" />
      <LineId Id="216" Count="3" />
      <LineId Id="224" Count="0" />
      <LineId Id="158" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="160" Count="0" />
      <LineId Id="137" Count="0" />
      <LineId Id="171" Count="0" />
      <LineId Id="139" Count="0" />
      <LineId Id="202" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="201" Count="0" />
      <LineId Id="140" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="69" Count="1" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_EmptyMachine">
      <LineId Id="56" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_StartCycle">
      <LineId Id="56" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="58" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_StartInit">
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_StopCycle">
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_StopDirect">
      <LineId Id="56" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.M_StopInit">
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_TopMold_OLD.Objects">
      <LineId Id="2" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="14" Count="2" />
      <LineId Id="51" Count="0" />
      <LineId Id="53" Count="1" />
      <LineId Id="113" Count="0" />
      <LineId Id="115" Count="1" />
      <LineId Id="114" Count="0" />
      <LineId Id="52" Count="0" />
      <LineId Id="104" Count="0" />
      <LineId Id="131" Count="5" />
      <LineId Id="105" Count="0" />
      <LineId Id="102" Count="1" />
      <LineId Id="49" Count="0" />
      <LineId Id="32" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="43" Count="2" />
      <LineId Id="34" Count="0" />
      <LineId Id="38" Count="0" />
      <LineId Id="46" Count="2" />
      <LineId Id="55" Count="0" />
      <LineId Id="72" Count="0" />
      <LineId Id="118" Count="0" />
      <LineId Id="125" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="95" Count="0" />
      <LineId Id="106" Count="1" />
      <LineId Id="76" Count="0" />
      <LineId Id="119" Count="0" />
      <LineId Id="126" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="108" Count="0" />
      <LineId Id="79" Count="1" />
      <LineId Id="120" Count="0" />
      <LineId Id="127" Count="0" />
      <LineId Id="81" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="109" Count="0" />
      <LineId Id="83" Count="1" />
      <LineId Id="121" Count="0" />
      <LineId Id="128" Count="0" />
      <LineId Id="85" Count="1" />
      <LineId Id="110" Count="0" />
      <LineId Id="87" Count="1" />
      <LineId Id="122" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="89" Count="1" />
      <LineId Id="111" Count="0" />
      <LineId Id="91" Count="1" />
      <LineId Id="123" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="93" Count="1" />
      <LineId Id="112" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>