<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4020.12">
  <POU Name="FB_ProductForming" Id="{6c67d5b1-edcf-45a7-aa63-ff8b75b379ff}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_ProductForming
VAR CONSTANT
	sCycleStateTextDef: ARRAY[0..25]  OF STRING :=	
		['State 0: Wait for Start',
		'State 1: Start wire spray',
		'State 2: Move pond up',
		'State 3: Pulp supply open when bubbling is done',
		'State 4: First vacuum time is finished, start 2nd vacuum time',
		'State 5: When first vacuum time is finished and 2nd started, move pond down',
		'State 6: Start flsh spray',
		'State 7: Close forming mold vacuum on transfermold position down',
		'State 8: Close time (time transfer is closed on forming)',
		'State 9: Switch blowoff on departing tranfer position',
		'State 10: ',
		'State 11: ',
		'State 12: ',
		'State 13: ',
		'State 14: ',
		'State 15: ',
		'State 16: ',
		'State 17: ',
		'State 18: ',
		'State 19: ',
		'State 20: [Paused] wait for start',
		'State 21: Stop direct state'
		];
	
	tCycleStateTimeouts: ARRAY[0..10] OF TIME := 
		[T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,
		T#10S];

	sInitStateTextDef: ARRAY[0..10] OF STRING :=
		['State 0: Wait for Start',
		'State 1: Move machinepond down',
		'State 2: Move the spraybar to the rest position',
		'State 3: Description Init State 3',
		'State 4: Description Init State 4',
		'State 5: Description Init State 5',
		'State 6: Description Init State 6',
		'State 7: Description Init State 7',
		'State 8: Description Init State 8',
		'State 9: Description Init State 9',
		'State 10: Stop init state'];

	tInitStateTimeouts: ARRAY[0..10] OF TIME := 
		[T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S,T#10S
		,T#10S];
END_VAR

VAR_INPUT
	mxResetCycleState			: BOOL;
	mxResetInitState			: BOOL;
	mxForceInit					: BOOL; (*Reset Calibration Flag*)
	mxReset						: BOOL;	(*Reset Error*)
	mxEmergency					: BOOL;
	mxAutoMode					: BOOL; (* TRUE = Automode *)
	mxStartCycle				: BOOL;
	mxEmptyMachine				: BOOL;
	mxFirstCycle				: BOOL;	
	mxStartTransferMoldVacuum	: BOOL;
	mxAfterBlowOffIsOn			: BOOL;
	ActualPositionTransfermold	: REAL;
	TransfermoldDownPosition	: REAL; // Position at which the transfermold thinks its down.
	TransferFormingPosOffset	: REAL; // Position offset on Transfer Down 
	TransfermoldMovingDown		: BOOL; // Transfermold is moving down if true.
	TransfermoldMovingUp		: BOOL;
	RHSR_Blocked 				: BOOL;
	
	(*Signals*)
	sgTransferIsDown			: BOOL;(* Reset in cycle *)
	sgAtPosition				: BOOL;
	sgTransferToMoveUp			: BOOL;	
	SgTransferToDownForming		: BOOL; (* Reset at main cycle *)
			
END_VAR
VAR_IN_OUT
	MachParameters				: ST_ProductFormingMachPar;
	SysParameters				: ST_ProductFormingSysPar;
	NewValue_MaxTimeSingleKO: BOOL;
END_VAR
VAR_OUTPUT
(*Errors*)
	Errors						: ST_ProductFormingErrors;
	mxErrorInit					: BOOL;		(* Error, stop immediately. Initialisation neccesary *)
	mxErrorStopinPos			: BOOL;		(* Error, stop in position. Only reset neccesary *)

(* State vars *)
	mxCycleBusy					: BOOL;
	mxInitBusy					: BOOL;
	
(* Vars *)
	mxInitialized				: BOOL;
	mxCilIsMovingIn				: BOOL;
	mxCilIsMovingOut			: BOOL;
	mxCilIsIn					: BOOL;
	mxCilIsOut					: BOOL;

(*Signals*)	
	mxSprayBarAtRestPos			: BOOL;
	sgFormingToPause			: BOOL;	
END_VAR
VAR
(* State vars *)
	miCycleState				: INT :=0;
	miCycleOldstate				: INT :=0;
	mxCycleTransitionState		: BOOL := FALSE;
	sCycleStateText				: STRING;
	exCycleTimeOut				: BOOL;		(* Timeout for the whole programcycle *)
	CycleTimeout				: TON;
	CycleTimeOutUpFlank			: R_TRIG;
	mxCycleTimeoutEnable		: BOOL;
	CycleTime					: REAL;
	mxCycleHasError				: BOOL;
	miStopState					: INT;
	mxStopCycle					: BOOL;
	mxStopDirect				: BOOL;
	
	miInitState					: INT	:=0;
	miInitOldstate				: INT	:=0;
	mxInitTransitionState		: BOOL	:= FALSE;
	sInitStateText				: STRING;
	exInitTimeOut				: BOOL;		(* Timeout for the whole programcycle *)
	InitTimeout					: TON;
	InitTimeOutUpFlank			: R_TRIG;
	mxInitTimeoutEnable			: BOOL;
	InitTime					: REAL;
	mxInitHasError				: BOOL;
	mxStartInit					: BOOL;
	mxStopInit					: BOOL;
	
(* Objects *)
(* Forming Mold *)
	MoldVacuum					: FB_MonoValveNoFdbck;							
	MoldBlowOff					: FB_MonoValveNoFdbck;
(* MachinePond *)
	PondHydraulicCilinder		: FB_HydraulicCilinder;
	FeedingPulp					: FB_MonoValve2Fdbck;
(* SprayBar *)	
	WireSprayValve				: FB_MonoValveNoFdbck;
	FlashSprayValve				: FB_MonoValveNoFdbck;
	SprayBarMotor				: FB_NCAxis;	

	ixSprayBar_FrontA			AT %I*: BOOL; //Zeestra 20190222
	ixSprayBar_BackA			AT %I*: BOOL; //Zeestra 20190222
	ixSprayBar_FrontB			AT %I*: BOOL; //Zeestra 20190222
	ixSprayBar_BackB			AT %I*: BOOL; //Zeestra 20190222
	
	TankLevel01					: FB_AnalogIn4_20mA;
	TankLevel02					: FB_AnalogIn4_20mA;
	
(* Vars *)
	T_PondUpWaitTime			: TON;
	T_FeedingPulp				: TON;
	T_Bubble					: TON;
	T_FirstVacuum				: TON;
	T_SecondVacuum				: TON;
	T_ThirdVacuum				: TON;
	T_FormingClosedTime 		: TON;
                        		                    		
	FormingVacuumTime			: TIME;              		
	miWireSprayInterval			: INT;
	mxMoveForward				: BOOL;
	miFlashSprayInterval		: INT;
                        	
	RT_ixPulpTank01				: R_TRIG;
	RT_ixPulpTank02				: R_TRIG;
	RT_FlashSprayStarted		: R_TRIG;
	RT_InitBusy					: R_TRIG;
	RT_SprayBarAtRestFront		: R_TRIG;	//Zeestra 20190222
	RT_SprayBarAtRestBack		: R_TRIG;	//Zeestra 20190222

	mxSprayBarAtRestPosFront	: BOOL;
	mxSprayBarAtRestPosBack		: BOOL;
	T_SprayDelayTime			: TON;
	T_MaxKOTimer				: TON;
	T_CurrentSpeedSingleKO		: TON;
	MaxTimeSingleKO				: REAL;
	CurrentSpeedSingleKO		: REAL;
	mxInPause					: BOOL;
	mxSpray						: BOOL;
	(* Interface signals pulp tanks, plup supply*)
	ixPulpTank01				AT %I*: BOOL;
	ixPulpTank02				AT %I*: BOOL;
	ixCssHasAnError				AT %I*: BOOL;
	SR_TanksAreAllmostEmpty		: SR;
	SR_CssHasError				: SR;
	SR_SprayBarAtFrontError		: SR;	//Zeestra 20190222
	SR_SprayBarAtBackError		: SR;	//Zeestra 20190222
	T_TransferUpWaitForForming: TON;
	WaitTimeFormingSC: REAL;
	ThirdVacuumTime: TIME;
	MeasureThirdVacuum: BOOL;
	RT_ThirdVacuum: R_TRIG;
	T_NoTankSelected: TON;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[Error();
Init();
Cycle();
Objects();

mxSprayBarAtRestPos:= mxSprayBarAtRestPosFront OR mxSprayBarAtRestPosBack;
	
mxSprayBarAtRestPosFront := ((SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionRestFront + 1) AND (SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionRestFront - 1));
mxSprayBarAtRestPosBack := ((SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionRestBack + 1) AND (SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionRestBack - 1));

RT_SprayBarAtRestFront(CLK := mxSprayBarAtRestPosFront); 	//Zeestra 20190222	
RT_SprayBarAtRestBack(CLK := mxSprayBarAtRestPosBack);		//Zeestra 20190222

IF (NOT mxCycleBusy AND NOT mxInitBusy AND mxAutoMode) THEN
	PondHydraulicCilinder.M_Move(End_Position:= MachParameters.MachinePond.PositionDownStop, Slow_Position:= MachParameters.MachinePond.PositionDownSlow);
END_IF]]></ST>
    </Implementation>
    <Action Name="Cycle" Id="{526db3bc-f668-474b-8e24-ef1ffb276674}">
      <Implementation>
        <ST><![CDATA[(*StateChange (Transition)  Signalgeneration*)
mxCycleTransitionState:=(miCycleState<>miCycleOldstate);
(*State text*)
sCycleStateText:=sCycleStateTextDef[miCycleState];
(*Timeout Watchdog for Statemachine*)
mxCycleTimeoutEnable:=NOT (miCycleState=0) AND NOT mxReset;

CycleTimeout(IN:=mxCycleTimeoutEnable, PT:=tCycleStateTimeouts[miCycleState] );
CycleTimeOutUpFlank(CLK:=CycleTimeout.Q );
miCycleOldstate:=miCycleState;

(*Set Errorbit when Raising edge of Timeout has been detected*)
IF CycleTimeOutUpFlank.Q THEN
	exCycleTimeOut:=TRUE; 			(* 1 Timeout for the whole programcycle *)
END_IF

(*Function to reset the Statemachine*)
IF mxResetCycleState THEN
	mxResetCycleState:= FALSE;
	mxInitialized := FALSE;
	miCycleState:=0;
	miCycleOldstate:=miCycleState;
	mxCycleTransitionState:=FALSE;

	mxStartTransferMoldVacuum:= FALSE;
	mxAfterBlowOffIsOn := FALSE;
	mxStartCycle:= FALSE;
	mxStopCycle:= FALSE;
	mxStopDirect:=FALSE;

	T_FirstVacuum.In := FALSE;
	T_SecondVacuum.IN:= FALSE;
	T_ThirdVacuum.IN := TRUE;
	
	T_PondUpWaitTime.IN:= FALSE;
	T_FeedingPulp.IN:= FALSE;
	T_Bubble.IN:= FALSE;
	
	T_SprayDelayTime.IN:= FALSE;
	T_MaxKOTimer.IN := FALSE;
	SprayBarMotor.M_Stop();
	
	sgTransferIsDown := FALSE;
	sgAtPosition :=FALSE;
	sgTransferToMoveUp :=FALSE;	
	SgTransferToDownForming :=FALSE; 
	
	MoldVacuum.M_Off();				
	MoldBlowOff.M_Off();
	FeedingPulp.M_Close();
END_IF

(*Definition State has Error*)
mxCycleHasError:=FALSE;

(* Set Forming vacuum time when switch over to next pulp tank,
Increase when flah spray is started*)
RT_ixPulpTank01(CLK:= ixPulpTank01);
RT_ixPulpTank02(CLK:= ixPulpTank02);
RT_InitBusy(CLK:= mxInitBusy);
RT_FlashSprayStarted(CLK:= miCycleState = 8);
	
IF RT_ixPulpTank01.Q OR RT_ixPulpTank02.Q OR RT_InitBusy.Q OR TRUE THEN // ----------------------------------------------------------------------------------- TRUE Weghalen wanneer wissel signaal actief
	FormingVacuumTime:= MachParameters.FormingMold.VacuumTime;
ELSIF RT_FlashSprayStarted.Q AND FALSE THEN								// ----------------------------------------------------------------------------------- IDEM voor false.
	IF ixPulpTank01 THEN
		FormingVacuumTime := FormingVacuumTime + (REAL_TO_TIME((TIME_TO_REAL(MachParameters.FormingMold.VacuumTimeIncr ) / 70.0) * (75 -  TankLevel01.EngUnits))); 
	ELSIF ixPulpTank02 THEN
		FormingVacuumTime := FormingVacuumTime + (REAL_TO_TIME((TIME_TO_REAL(MachParameters.FormingMold.VacuumTimeIncr ) / 70.0) * (75 -  TankLevel02.EngUnits))); 
	END_IF
END_IF


// 			Timers			//
T_FeedingPulp(IN:= , PT:= MachParameters.MachinePond.FeedingPulpTime); 
T_Bubble(IN:= , PT:= MachParameters.FormingMold.BubbleTime);
T_FormingClosedTime(IN:= , PT:=MachParameters.FormingMold.ClosedTime);
T_SprayDelayTime(IN:=, PT:=MachParameters.FormingMold.SecondVacuum - MachParameters.SprayBar.SprayDelayTimeOffset);
T_MaxKOTimer(IN:=, PT:=T#250S);
T_CurrentSpeedSingleKO(IN:=, PT:=T#250S);
T_TransferUpWaitForForming(PT:=T#250S);
T_FirstVacuum(IN:= , PT:= FormingVacuumTime);
T_SecondVacuum(IN:= , PT:= MachParameters.FormingMold.SecondVacuum);
T_ThirdVacuum(IN:=, PT:=MachParameters.FormingMold.ThirdVacuum);
RT_ThirdVacuum(CLK:=MeasureThirdVacuum);
	
// Signal the transfermold that it can start moving down afet T2 has elapsed
IF T_SecondVacuum.Q THEN	
	T_SecondVacuum.IN:= FALSE;
	T_ThirdVacuum.IN := TRUE;
	SgTransferToDownForming:= TRUE; // send transfermold down
END_IF

// Calculate the time third vacuum was active
IF RT_ThirdVacuum.Q THEN
	ThirdVacuumTime := T_ThirdVacuum.ET;
	T_ThirdVacuum.IN := FALSE;
END_IF

// Close feeding pulp valve after timer is done
IF T_FeedingPulp.Q THEN 
	T_FeedingPulp.IN := FALSE;
	FeedingPulp.M_Close(); 
END_IF
			
(*Cycle State's*)
CASE miCycleState OF

	0:  //(P) Await start signal from speed control 	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF mxStopCycle AND mxEmptyMachine THEN
			mxStopCycle:= FALSE;
			mxEmptyMachine:= FALSE;
			T_MaxKOTimer.IN := FALSE;
			MaxTimeSingleKO := 0.0;
			miCycleState:= 0;
		ELSIF mxStopCycle THEN
			mxStopCycle:= FALSE;
			miStopState:= miCycleState;
			miCycleState:= 20;
		ELSIF mxStartCycle THEN
			mxStartCycle:= FALSE;
			mxStartTransferMoldVacuum:= FALSE;
			
			//Time between Transfer Ready and Start From speedcontrol
			WaitTimeFormingSC := TIME_TO_REAL(T_TransferUpWaitForForming.ET);
			T_TransferUpWaitForForming.IN := FALSE;
			
			// Start theoretical max speed timer for ko calc.
			T_MaxKOTimer.IN := TRUE;	

			// Calculate its current speed
			CurrentSpeedSingleKO := TIME_TO_REAL(T_CurrentSpeedSingleKO.ET);
			T_CurrentSpeedSingleKO.IN :=FALSE;
			
			mxMoveForward:= FALSE;				(* Check if at rest back position, then move forward, else move backward *)
			IF SprayBarMotor.ActPosition < MachParameters.SprayBar.PositionRestBack + 5 AND SprayBarMotor.ActPosition > MachParameters.SprayBar.PositionRestBack - 5 THEN
				mxMoveForward:= TRUE;
			END_IF	
			(* If parameter = 0 then do not perform spray, if 1 then always perform spray, other: perform every x time *)
			IF MachParameters.SprayBar.WireSprayInterval > 0 AND ((miWireSprayInterval = MachParameters.SprayBar.WireSprayInterval) OR MachParameters.SprayBar.WireSprayInterval = 1) THEN
				miWireSprayInterval:= miWireSprayInterval - 1;		
				miCycleState:= 1;
				mxSpray:=TRUE;
			ELSE
				miWireSprayInterval:= miWireSprayInterval - 1;
				miCycleState:=1;
			END_IF
			IF miWireSprayInterval <=0 THEN miWireSprayInterval:= MachParameters.SprayBar.WireSprayInterval; END_IF		//Write to par if is lower then 0
		END_IF
		
	
	1:  //(NP) Start Wire Spray
		T_CurrentSpeedSingleKO.IN :=TRUE;

		IF mxMoveForward THEN
			IF SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionStartWireFwd AND mxSpray THEN
				WireSprayValve.M_On();
			END_IF
			IF SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionStopWireFwd AND mxSpray THEN
				WireSprayValve.M_Off();
			END_IF
		ELSE
			IF SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionStartWireBkw AND mxSpray THEN
				WireSprayValve.M_On();
			END_IF
			IF SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionStopWireBkw AND mxSpray THEN
				WireSprayValve.M_Off();
			END_IF
		END_IF
			
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF mxMoveForward  THEN
			IF SprayBarMotor.M_MoveAbsolute(Velocity:= MachParameters.SprayBar.AutoVelocity, Position:= MachParameters.SprayBar.PositionRestFront) THEN
				miCycleState:= 2;	
			END_IF
		ELSIF NOT mxMoveForward THEN
			IF SprayBarMotor.M_MoveAbsolute(Velocity:= MachParameters.SprayBar.AutoVelocity, Position:= MachParameters.SprayBar.PositionRestBack) THEN
				miCycleState:= 2;	
			END_IF
		END_IF
	
	2:	//(NP) Move pond up 
		IF (PondHydraulicCilinder.ActPosition > (MachParameters.MachinePond.PositionUpStop - MachParameters.MachinePond.BubblingPositionOffset)) THEN
			IF MachParameters.FormingMold.BubbleTime = T#0MS THEN
				T_Bubble.IN:= TRUE;
			ELSE
				IF MoldBlowOff.M_On() THEN T_Bubble.IN:= TRUE; END_IF  // When the machine pond is almost up -> Start bubbling (open valve)
			END_IF
		END_IF
		
		IF (PondHydraulicCilinder.ActPosition > (MachParameters.MachinePond.PositionUpStop - MachParameters.MachinePond.PulpFeedingOffset)) THEN		
			FeedingPulp.M_Open();  // open feeding valve
			T_FeedingPulp.IN:= TRUE;	
		END_IF
	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF PondHydraulicCilinder.M_Move(End_Position:= MachParameters.MachinePond.PositionUpStop, Slow_Position:= MachParameters.MachinePond.PositionUpSlow) THEN
			miCycleState:= 3;
		END_IF
	
	3:	//(NP) pulp supply open when bubbling is done.		
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF T_Bubble.Q THEN 
			T_Bubble.IN := FALSE;
			MoldBlowOff.M_Off();	
		

			//Start first vacuum
			MoldVacuum.M_On();	// open the vacuum valve and start first vac. timer.
			T_FirstVacuum.IN := TRUE;	
			
			miCycleState:= 4;
		END_IF	
		
	4:	//(NP) First vacuum time is finished, start 2nd vacuum time	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;	
		ELSIF T_FirstVacuum.Q THEN
			T_FirstVacuum.IN := FALSE;
			T_SecondVacuum.IN := TRUE;	
			T_SprayDelayTime.IN := TRUE;
			miCycleState:= 5;
		END_IF		
	
	5:	//(NP) When first vacuum is finished and 2nd started, move pond down.
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF PondHydraulicCilinder.M_Move(End_Position:= MachParameters.MachinePond.PositionDownStop, Slow_Position:= MachParameters.MachinePond.PositionDownSlow) THEN	
			IF T_SprayDelayTime.Q THEN		// Start the spraying (2nd vacuum time - Spray delay offset).
				T_SprayDelayTime.IN :=FALSE;	
			
				mxMoveForward:= FALSE;
				IF SprayBarMotor.ActPosition < MachParameters.SprayBar.PositionRestBack + 5 AND SprayBarMotor.ActPosition > MachParameters.SprayBar.PositionRestBack - 5 THEN
					mxMoveForward:= TRUE;
				END_IF	
				
				(* If parameter = 0 then do not perform spray, if 1 then always perform spray, other: perform every x time *)
				IF MachParameters.SprayBar.FlashSprayInterval > 0 AND ((miFlashSprayInterval = MachParameters.SprayBar.FlashSprayInterval) OR MachParameters.SprayBar.FlashSprayInterval = 1) THEN
					miFlashSprayInterval:= miFlashSprayInterval - 1;
					mxSpray:=TRUE;
					miCycleState:= 6; 
				ELSE
					miFlashSprayInterval:= miFlashSprayInterval - 1;		
					mxSpray:=FALSE;
					miCycleState:= 6;			
				END_IF
				IF miFlashSprayInterval <=0 THEN miFlashSprayInterval:= MachParameters.SprayBar.FlashSprayInterval; END_IF 	// Write to par if is lower then 0
			END_IF
		END_IF
	
	6: 	//(NP) Start flash spray 
		IF mxMoveForward THEN
			IF SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionStartFlashFwd AND mxSpray THEN
				FlashSprayValve.M_On();
			END_IF
			IF SprayBarMotor.ActPosition >= MachParameters.SprayBar.PositionStopFlashFwd AND mxSpray THEN
				FlashSprayValve.M_Off();
			END_IF
		ELSE
			IF SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionStartFlashBkw AND mxSpray THEN
				FlashSprayValve.M_On();
			END_IF
			IF SprayBarMotor.ActPosition <= MachParameters.SprayBar.PositionStopFlashBkw AND mxSpray THEN
				FlashSprayValve.M_Off();
			END_IF
		END_IF

		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF mxMoveForward THEN
			IF SprayBarMotor.M_MoveAbsolute(Velocity:= MachParameters.SprayBar.AutoVelocity, Position:= MachParameters.SprayBar.PositionRestFront) THEN
				miCycleState:= 7;	
			END_IF
		ELSIF NOT mxMoveForward THEN
			IF SprayBarMotor.M_MoveAbsolute(Velocity:= MachParameters.SprayBar.AutoVelocity, Position:= MachParameters.SprayBar.PositionRestBack) THEN
				miCycleState:= 7;	
			END_IF
		END_IF
		
	7:	//(P) Close forming mold vacuum valve on the way down of transfermold.	
		IF TransfermoldMovingDown AND (ActualPositionTransfermold < (TransfermoldDownPosition + MachParameters.FormingMold.StopVacuumAtTrPosOffset)) THEN
			MeasureThirdVacuum := TRUE;			
			MoldVacuum.M_Off();
		END_IF
		
		IF TransfermoldMovingDown AND (ActualPositionTransfermold < (TransfermoldDownPosition + MachParameters.FormingMold.StartBlowOffAtTrPosOffset)) THEN				
			MoldBlowoff.M_On();
		END_IF

		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF mxStopCycle AND  NOT TransfermoldMovingDown THEN
			mxStopCycle:= FALSE;
			miStopState:= miCycleState;
			MoldVacuum.M_Off();				// Close the vacuum valve when it was still open.
			miCycleState:= 20;
		ELSIF sgTransferIsDown THEN
			sgTransferIsDown:= FALSE;
			MeasureThirdVacuum := FALSE;
			miCycleState:= 8;
		END_IF
		
	8:	//(NP) Close time (time the transfer is closed on the formingmold).
		T_FormingClosedTime.IN:= TRUE;

		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF T_FormingClosedTime.Q THEN	// Signal main cycle that transfer to start moving up again.
			T_FormingClosedTime.In:= FALSE;
			sgTransferToMoveUp:=TRUE;
			miCycleState:= 9;
		END_IF

	
	9:	//(NP) Switch blowoff due to transfer position 
	//	IF TransfermoldMovingUp AND 
	//	(ActualPositionTransfermold > (TransfermoldDownPosition + MachParameters.FormingMold.StartBlowOffAtTrPosOffset)) THEN				
	//		MoldBlowoff.M_On();
	//	END_IF
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF TransfermoldMovingUp AND 
		(ActualPositionTransfermold > (TransfermoldDownPosition + MachParameters.FormingMold.StopBlowOffAtTrPosOffset)) THEN				
			MoldBlowoff.M_Off();
			IF (ActualPositionTransfermold > (TransfermoldDownPosition + TransferFormingPosOffset)) THEN
				T_TransferUpWaitForForming.IN := TRUE;
				MaxTimeSingleKO := TIME_TO_REAL(T_MaxKOTimer.ET);
				NewValue_MaxTimeSingleKO := TRUE;
				T_MaxKOTimer.IN := FALSE;
				miCycleState:= 0;
			END_IF
		END_IF
		
	20: // Pause state
		FeedingPulp.M_Close();				// on controlled stop, close the pulpfeeding valve to prevent flooding.
	
		IF mxStopDirect THEN
			mxStopDirect:= FALSE;
			miCycleState:= 21;
		ELSIF mxStartCycle THEN
			mxStartCycle:= FALSE;
			miCycleState:= miStopState;
			miStopState := 0;				// change made for speed control
			T_MaxKOTimer.IN := FALSE;	
		END_IF
	
	21: // Direct stop state
		T_PondUpWaitTime.IN:= FALSE;
		T_FeedingPulp.IN:= FALSE;
		T_Bubble.IN:= FALSE;
		
		T_FirstVacuum.IN:= FALSE;
		T_SecondVacuum.IN:= FALSE;
		T_ThirdVacuum.IN := FALSE;
		
		T_CurrentSpeedSingleKO.IN :=TRUE;
		T_MaxKOTimer.IN := FALSE;
		
		CurrentSpeedSingleKO := 0;
		MaxTimeSingleKO := 0;
		
		MoldVacuum.M_Off();				
		MoldBlowOff.M_Off();

		PondHydraulicCilinder.M_StopDirect();
		FeedingPulp.M_Close();
	
		WireSprayValve.M_Off();
		FlashSprayValve.M_Off();
		SprayBarMotor.M_Stop();

		mxStartCycle:= FALSE;
		mxStopCycle:= FALSE;
		mxStopDirect:=FALSE;
		mxInitialized:= FALSE;

		sgTransferIsDown := FALSE;
		sgAtPosition :=FALSE;
		sgTransferToMoveUp :=FALSE;	
		SgTransferToDownForming :=FALSE; 
		
		miCycleState:= 0;
END_CASE

mxCycleBusy:= miCycleState<>0 AND NOT (miCycleState=20);
mxInPause := (miCycleState = 20);
sgFormingToPause := (miStopState = 7);]]></ST>
      </Implementation>
    </Action>
    <Action Name="Error" Id="{4f3b0574-9e69-4fac-a234-8776369a5bc2}">
      <Implementation>
        <ST><![CDATA[T_NoTankSelected(IN:=NOT ixPulpTank01 AND NOT ixPulpTank02, PT:=T#10S);

SR_TanksAreAllmostEmpty(SET1:=((TankLevel01.EngUnits < 5) AND (TankLevel02.EngUnits < 5)) OR (T_NoTankSelected.Q), RESET:=mxReset, Q1=>Errors.LowLvlOnBothTanks);  
SR_CssHasError(SET1:=ixCssHasAnError, Reset:=mxReset, Q1=>Errors.CssHasError);

SR_SprayBarAtFrontError(SET1:=(RT_SprayBarAtRestFront.Q AND (NOT ixSprayBar_FrontA OR NOT ixSprayBar_FrontB)), Reset:=mxReset, Q1=>Errors.SprayBar.PositionAtFrontError);	//Zeestra 20190222	
SR_SprayBarAtBackError(SET1:=(RT_SprayBarAtRestBack.Q AND (NOT ixSprayBar_BackA OR NOT ixSprayBar_BackB)), Reset:=mxReset, Q1=>Errors.SprayBar.PositionAtBackError);			//Zeestra 20190222

(*************************************************************************************************)
mxErrorInit:= Errors.HydraulicPump.Thermal OR Errors.SprayBar.PositionAtFrontError OR Errors.SprayBar.PositionAtBackError; //Zeestra 20190222: Added Errors spraybar;
mxErrorStopinPos:= FALSE;
]]></ST>
      </Implementation>
    </Action>
    <Action Name="Init" Id="{062d9d4a-b42a-4cc4-9cdf-ea6dec5d4990}">
      <Implementation>
        <ST><![CDATA[(*StateChange (Transition)  Signalgeneration*)
mxInitTransitionState:=(miInitState<>miInitOldstate);
(*State text*)
sInitStateText:=sInitStateTextDef[miInitState];
(*Timeout Watchdog for Statemachine*)
mxInitTimeoutEnable:=NOT (miInitState=0)  AND NOT mxReset;

InitTimeout(IN:=mxInitTimeoutEnable  , PT:=tInitStateTimeouts[miInitState] );
InitTimeOutUpFlank(CLK:=InitTimeout.Q );
miInitOldstate:=miInitState;

(*Set Errorbit when Raising edge of Timeout has been detected*)
IF InitTimeOutUpFlank.Q THEN
	exInitTimeOut:=TRUE; 			(* 1 Timeout for the whole programcycle *)
END_IF

(*Function to reset the Statemachine*)
IF mxResetInitState THEN
	mxResetInitState:=FALSE;
	mxInitialized:= FALSE;
	miInitState:=0;
	miInitOldstate:=miInitState;
	mxInitTransitionState:=FALSE;
	
	mxStartInit:= FALSE;
	mxStopInit:= FALSE;
END_IF

(*Definition State has Error*)
mxInitHasError:=FALSE;

CASE miInitState OF
	0:
		IF mxStartInit THEN
			mxStartInit:= FALSE;
			mxInitialized:= FALSE;

		(* Forming Mold *)
			MoldVacuum.M_Off();				
			MoldBlowOff.M_Off();
		(* MachinePond *)
			FeedingPulp.M_Close();
		(* SprayBar *)	
			WireSprayValve.M_Off();
			FlashSprayValve.M_Off();
			(* AT init write intervals *)
			miWireSprayInterval:= MachParameters.SprayBar.WireSprayInterval;
			miFlashSprayInterval:= MachParameters.SprayBar.FlashSprayInterval;
			
			miInitState:= 1;
		END_IF

	(* Move Pond down *)	
	1:
		IF mxStopInit THEN
			mxStopInit:= FALSE;
			
			miInitState:= 10;			
		ELSIF PondHydraulicCilinder.M_Move(End_Position:= MachParameters.MachinePond.PositionDownStop, Slow_Position:= MachParameters.MachinePond.PositionDownSlow) THEN
			
			miInitState:= 2;
		END_IF				

	(* Move spraybar to rest position *)	
	2:
		IF mxStopInit THEN
			mxStopInit:= FALSE;
			
			miInitState:= 10;			
		ELSIF SprayBarMotor.M_MoveAbsolute(Position:= MachParameters.SprayBar.PositionRestBack, Velocity:= MachParameters.SprayBar.AutoVelocity) THEN
			mxInitialized:= TRUE;
			
			// stop the pump en cylinder of the pond
			PondHydraulicCilinder.M_Stop();
			
			miInitState:= 0;
		END_IF

	10:
		PondHydraulicCilinder.M_Stop();
		SprayBarMotor.M_Stop();

		mxStartInit:= FALSE;
		mxStopInit:= FALSE;
		mxInitialized:= FALSE;
		
		miInitState:= 0;
END_CASE

mxInitBusy:= miInitState<>0;
]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_EmptyMachine" Id="{e9b5fe1c-013e-4046-be5a-c3180911caa1}">
      <Declaration><![CDATA[METHOD M_EmptyMachine
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxCycleBusy THEN
	mxEmptyMachine:= TRUE;	
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartCycle" Id="{f36509da-31c1-47fb-b307-ba3cb6b804a0}">
      <Declaration><![CDATA[METHOD M_StartCycle
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT mxCycleBusy THEN
	mxStartCycle:= TRUE;
	mxStopCycle:=False;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StartInit" Id="{fa9a46d1-2d66-418c-a692-75f873825878}">
      <Declaration><![CDATA[METHOD M_StartInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF NOT mxInitBusy THEN 
	mxStartInit:= TRUE;
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopCycle" Id="{7c382542-ac35-4535-b2de-407cddeb2d76}">
      <Declaration><![CDATA[METHOD M_StopCycle
]]></Declaration>
      <Implementation>
        <ST><![CDATA[mxStopCycle:= TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopDirect" Id="{857b5cd7-108e-403a-9993-f735709dbf4f}">
      <Declaration><![CDATA[METHOD M_StopDirect
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxCycleBusy THEN
	mxStopDirect:= TRUE;	
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_StopInit" Id="{8eb1a74f-44e0-45f5-95dd-8199c37ac87e}">
      <Declaration><![CDATA[METHOD M_StopInit
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
IF mxInitBusy THEN 
	mxStopInit:= TRUE; 
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Action Name="Objects" Id="{53066a3c-b8a1-46d0-b978-121c7b7ff810}">
      <Implementation>
        <ST><![CDATA[
MoldVacuum(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency);

MoldBlowOff(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency);
	
PondHydraulicCilinder(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency,
	EndPositionIn:= MachParameters.MachinePond.PositionDownStop,
	EndPositionOut:= MachParameters.MachinePond.PositionUpStop,
	OffsetPosition:= MachParameters.MachinePond.PositionOffsetZero,
	OffsetAlarmPosition:= MachParameters.MachinePond.PositionOffsetAlarm,
	PositionPhysicalMeasured:=MachParameters.MachinePond.PositionPhyscialMeasured,
	PositionPhysicalMeasuredOld:=MachParameters.MachinePond.PositionPhysicalMeasuredOld,
	PhysicalOffsetPos:=MachParameters.MachinePond.PhysicalOffsetPos,
	RHSR_Blocked := RHSR_Blocked,
	SlowValveIsInverted := FALSE,
	mxIsVertical := TRUE,
	mxIsMovingIn=> mxCilIsMovingIn, 
	mxIsMovingOut=> mxCilIsMovingOut, 
	mxIsIn=> mxCilIsIn, 
	mxIsOut=> mxCilIsOut,
	Errors=> );


FeedingPulp(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency,
	MachParameters:= MachParameters.MachinePond.FeedingPulp);

WireSprayValve(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency);

FlashSprayValve(
	mxReset:= mxReset,
	mxAutoMode:= mxAutoMode,
	mxEmergency:= mxEmergency);

SprayBarMotor(
	mxEnable:= NOT mxEmergency, 
	mxInhibit:=FALSE , 
	mxAutoMode:= mxAutoMode, 
	mxReset:= mxreset, 
	mxEmergency:= mxEmergency, 
	mxSimulation:= , 
	mxFirstCycle:= mxFirstCycle, 
	mxResetCycle:= , 
	mxDisable_Positive:=RHSR_Blocked , 
	mxDisable_Negative:=FALSE , 
	mxBlockHMI:=FALSE , 
	mrOverride:= 100,
	mrHomePos:=MachParameters.SprayBar.PositionHome,
	mrVelocityManual:= MachParameters.SprayBar.ManualVelocity, 
	SysParameters:= SysParameters.SprayBar, 
	mxCycleBusy=> , 
	mxRunning=> , 
	Errors=> errors.SprayBar.NCAxisError, 
	Error=> , 
	ErrorStop=> , 
	axStatus=> , 
	ActPosition=> , 
	ActVelocity=> , 
	axAxis=> );
	
TankLevel01(
	EngLowLimit:= 0, 
	EngUpperLimit:= 100, 
	maOut=> , 
	EngUnits=> , 
	EngFilt=> , 
	OverRange=> , 
	UnderRange=> );
	
TankLevel02(
	EngLowLimit:= 0, 
	EngUpperLimit:= 100, 
	maOut=> , 
	EngUnits=> , 
	EngFilt=> , 
	OverRange=> , 
	UnderRange=> );




	
]]></ST>
      </Implementation>
    </Action>
    <LineIds Name="FB_ProductForming">
      <LineId Id="318" Count="0" />
      <LineId Id="321" Count="0" />
      <LineId Id="319" Count="1" />
      <LineId Id="455" Count="0" />
      <LineId Id="458" Count="0" />
      <LineId Id="753" Count="0" />
      <LineId Id="456" Count="0" />
      <LineId Id="942" Count="2" />
      <LineId Id="1141" Count="3" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.Cycle">
      <LineId Id="2013" Count="44" />
      <LineId Id="2407" Count="2" />
      <LineId Id="2412" Count="0" />
      <LineId Id="2058" Count="63" />
      <LineId Id="2405" Count="0" />
      <LineId Id="2122" Count="138" />
      <LineId Id="2414" Count="0" />
      <LineId Id="2261" Count="51" />
      <LineId Id="2406" Count="0" />
      <LineId Id="2313" Count="49" />
      <LineId Id="2418" Count="0" />
      <LineId Id="2363" Count="41" />
      <LineId Id="2416" Count="0" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.Error">
      <LineId Id="16" Count="1" />
      <LineId Id="12" Count="0" />
      <LineId Id="14" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="5" Count="2" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.Init">
      <LineId Id="36" Count="18" />
      <LineId Id="128" Count="0" />
      <LineId Id="55" Count="1" />
      <LineId Id="127" Count="0" />
      <LineId Id="57" Count="0" />
      <LineId Id="91" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="60" Count="3" />
      <LineId Id="4" Count="0" />
      <LineId Id="6" Count="0" />
      <LineId Id="69" Count="0" />
      <LineId Id="86" Count="0" />
      <LineId Id="88" Count="0" />
      <LineId Id="90" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="100" Count="2" />
      <LineId Id="105" Count="0" />
      <LineId Id="107" Count="3" />
      <LineId Id="117" Count="2" />
      <LineId Id="89" Count="0" />
      <LineId Id="87" Count="0" />
      <LineId Id="84" Count="0" />
      <LineId Id="97" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="31" Count="1" />
      <LineId Id="26" Count="0" />
      <LineId Id="96" Count="0" />
      <LineId Id="28" Count="2" />
      <LineId Id="111" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="73" Count="0" />
      <LineId Id="80" Count="3" />
      <LineId Id="74" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="121" Count="1" />
      <LineId Id="131" Count="0" />
      <LineId Id="76" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="115" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="116" Count="0" />
      <LineId Id="129" Count="0" />
      <LineId Id="124" Count="0" />
      <LineId Id="130" Count="0" />
      <LineId Id="64" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="67" Count="1" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_EmptyMachine">
      <LineId Id="56" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_StartCycle">
      <LineId Id="8" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="46" Count="0" />
      <LineId Id="44" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_StartInit">
      <LineId Id="8" Count="0" />
      <LineId Id="22" Count="1" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_StopCycle">
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_StopDirect">
      <LineId Id="56" Count="0" />
      <LineId Id="18" Count="0" />
      <LineId Id="36" Count="0" />
      <LineId Id="7" Count="0" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.M_StopInit">
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_ProductForming.Objects">
      <LineId Id="292" Count="29" />
      <LineId Id="327" Count="5" />
      <LineId Id="339" Count="23" />
      <LineId Id="409" Count="0" />
      <LineId Id="408" Count="0" />
      <LineId Id="363" Count="9" />
      <LineId Id="412" Count="17" />
      <LineId Id="376" Count="0" />
      <LineId Id="410" Count="1" />
      <LineId Id="390" Count="0" />
      <LineId Id="373" Count="1" />
      <LineId Id="1" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>